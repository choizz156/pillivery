plugins {
    id "org.asciidoctor.jvm.convert" version "3.3.2"
}

configurations {
    asciidoctorExt
}


ext { // 전역 변수
    snippetsDir = file('build/generated-snippets')
    springBootAdminVersion = "2.7.4"
}

dependencies {


    //db
    runtimeOnly 'com.h2database:h2'

    //jpa
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    //expiringMap
    implementation 'net.jodah:expiringmap:0.5.11'

    implementation 'com.google.guava:guava:33.2.1-jre'


    //security test
    testImplementation 'org.springframework.security:spring-security-test'

    //restdocs
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'org.springframework.restdocs:spring-restdocs-restassured'

    //rest-assured-mockmvc
    testImplementation 'io.rest-assured:spring-mock-mvc:4.5.1'
    testImplementation 'io.rest-assured:rest-assured-all'

    //fixturemonkey
    testImplementation 'com.navercorp.fixturemonkey:fixture-monkey-starter:1.0.14'
    testImplementation 'com.navercorp.fixturemonkey:fixture-monkey-javax-validation:1.0.14'
    testImplementation 'com.navercorp.fixturemonkey:fixture-monkey-jackson:1.0.14'

    implementation 'de.codecentric:spring-boot-admin-starter-server'

    //lombok
    testAnnotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'

    testImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
}
test {
    useJUnitPlatform()
    outputs.dir snippetsDir
}

asciidoctor.doFirst {
    delete file('src/main/resources/static/docs')
}

asciidoctor {
    dependsOn test
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'

    sources { // 특정 파일만 html로 만든다.
        include("**/index.adoc")
    }

    baseDirFollowsSourceFile() // 다른 adoc 파일을 include 할 때 경로를 baseDir로 맞춘다.

}

bootJar {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}") {
        into 'static/docs'
    }
}

dependencyManagement {
    imports {
        mavenBom "de.codecentric:spring-boot-admin-dependencies:$springBootAdminVersion"
    }
}
